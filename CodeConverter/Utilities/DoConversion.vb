' Licensed to the .NET Foundation under one or more agreements.
' The .NET Foundation licenses this file to you under the MIT license.
' See the LICENSE file in the project root for more information.

Imports System.Diagnostics.CodeAnalysis
Imports System.Threading
Imports CSharpToVBConverter.ToVisualBasic
Imports Microsoft.CodeAnalysis
Imports ProgressReportLibrary
Imports CS = Microsoft.CodeAnalysis.CSharp
Imports VB = Microsoft.CodeAnalysis.VisualBasic

Namespace CSharpToVBConverter
    Public Module DoConversion
        Friend s_thisLock As New Object

        Private Function GetDefaultVersionForLanguage(language As String) As Integer
            If language Is Nothing Then
                Throw New ArgumentNullException(NameOf(language))
            End If
            If language.StartsWith("cs", StringComparison.OrdinalIgnoreCase) Then
                Return CS.LanguageVersion.CSharp8
            End If
            If language.StartsWith("vb", StringComparison.OrdinalIgnoreCase) Then
                Return VB.LanguageVersion.Latest
            End If
            Throw New ArgumentException($"{language} not supported!")
        End Function

        Public Function ConvertInputRequest(RequestToConvert As ConvertRequest, DefaultVBOptions As DefaultVBOptions, CSPreprocessorSymbols As List(Of String), VBPreprocessorSymbols As List(Of KeyValuePair(Of String, Object)), OptionalReferences() As MetadataReference, ReportException As Action(Of Exception), mProgress As IProgress(Of ProgressReport), CancelToken As CancellationToken) As ConversionResult
            Dim fromLanguage As String = LanguageNames.CSharp
            Dim toLanguage As String = LanguageNames.VisualBasic
            Dim codeWithOptions As CodeWithOptions = New CodeWithOptions(RequestToConvert).SetFromLanguageVersion(GetDefaultVersionForLanguage("cs")).SetToLanguageVersion(GetDefaultVersionForLanguage("vb"))
            SyncLock RequestToConvert.UsedStacks
                RequestToConvert.UsedStacks.Push(New Dictionary(Of String, SymbolTableEntry)(StringComparer.Ordinal))
            End SyncLock
            If RequestToConvert.SourceCode Is Nothing Then
                Throw New Exception($"{NameOf(RequestToConvert.SourceCode)} should not be Nothing")
            End If
            If OptionalReferences Is Nothing Then
                Throw New ArgumentNullException(NameOf(OptionalReferences))
            End If
            Dim tree As SyntaxTree = ParseCSharpSource(RequestToConvert.SourceCode, CSPreprocessorSymbols)
            Dim csOptions As CS.CSharpCompilationOptions =
                New CS.CSharpCompilationOptions(
                                outputKind:=Nothing,
                                reportSuppressedDiagnostics:=False,
                                moduleName:=Nothing,
                                mainTypeName:=Nothing,
                                scriptClassName:=Nothing,
                                usings:=Nothing,
                                OptimizationLevel.Debug,
                                checkOverflow:=False,
                                allowUnsafe:=True,
                                cryptoKeyContainer:=Nothing,
                                cryptoKeyFile:=Nothing,
                                cryptoPublicKey:=Nothing,
                                delaySign:=Nothing,
                                Platform.AnyCpu,
                                ReportDiagnostic.Default,
                                warningLevel:=4,
                                specificDiagnosticOptions:=Nothing,
                                concurrentBuild:=True,
                                deterministic:=False,
                                xmlReferenceResolver:=Nothing,
                                sourceReferenceResolver:=Nothing,
                                metadataReferenceResolver:=Nothing,
                                assemblyIdentityComparer:=Nothing,
                                strongNameProvider:=Nothing,
                                publicSign:=False
                                )
            Dim compilation As Compilation =
                CS.CSharpCompilation.Create(assemblyName:=NameOf(Conversion),
                                            {tree},
                                            OptionalReferences,
                                            csOptions
                                            )
            Try
                Dim sourceTree As CS.CSharpSyntaxNode = DirectCast(tree.GetRoot(CancelToken), CS.CSharpSyntaxNode)
                If RequestToConvert.SkipAutoGenerated AndAlso
                    sourceTree.SyntaxTree.IsGeneratedCode(Function(t As SyntaxTrivia) As Boolean
                                                              Return t.IsComment OrElse t.IsRegularOrDocComment
                                                          End Function, CancelToken) Then
                    Return New ConversionResult(Array.Empty(Of Exception))
                Else
                    Dim convertedNode As VB.VisualBasicSyntaxNode =
                            sourceTree.DoConversion(
                                compilation.GetSemanticModel(tree, ignoreAccessibility:=True),
                                DefaultVBOptions,
                                codeWithOptions.Request.SkipAutoGenerated,
                                ReportException,
                                mProgress,
                                CancelToken
                                )
                    RequestToConvert.UsedStacks.Clear()
                    Return New ConversionResult(convertedNode, LanguageNames.CSharp, LanguageNames.VisualBasic, VBPreprocessorSymbols)
                End If
            Catch ex As OperationCanceledException
                Return New ConversionResult(Array.Empty(Of Exception))
            Catch ex As Exception
                Return New ConversionResult(ex)
            End Try
        End Function

        <ExcludeFromCodeCoverage>
        <Obsolete("Don't use this routine any more. Use the new one instead to specify what 'Options' to include in output, use 'New DefaultVBOptions' to get the legacy behavior.")>
        Public Function ConvertInputRequest(RequestToConvert As ConvertRequest, CSPreprocessorSymbols As List(Of String), VBPreprocessorSymbols As List(Of KeyValuePair(Of String, Object)), OptionalReferences() As MetadataReference, ReportException As Action(Of Exception), mProgress As IProgress(Of ProgressReport), CancelToken As CancellationToken) As ConversionResult
            Return ConvertInputRequest(RequestToConvert, New DefaultVBOptions, CSPreprocessorSymbols, VBPreprocessorSymbols, OptionalReferences, ReportException, mProgress, CancelToken)
        End Function

    End Module
End Namespace
